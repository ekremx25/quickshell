import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import "../../../Widgets"
import "../../../Services"

Rectangle {
    id: workspaceRoot
    required property string monitorName
    property var tagIconCache: ({})
    property var config: ({ format: "arabic", style: "fill", transparent: false, activeColor: "" })
    property string style: config.style || "fill"
    property bool isTransparent: config.transparent === true
    property color activeColor: Theme.workspacesColor

    // DMS √∂zelliklerini bar_config.json'dan oku
    property bool showApps: config.showApps !== false
    property bool groupApps: config.groupApps !== false
    property bool scrollEnabled: config.scrollEnabled !== false
    property int iconSize: config.iconSize || 20
    
    // Mouse scroll biriktirici
    property real mouseAccumulator: 0
    property bool scrollInProgress: false
    
    Timer {
        id: scrollCooldown
        interval: 100
        onTriggered: workspaceRoot.scrollInProgress = false
    }

    // Ana arka plan ≈üeffaf, sadece i√ßindeki kutucuklar g√∂r√ºnecek
    color: "transparent"
    border.width: 0

    implicitHeight: 34
    implicitWidth: wsRow.implicitWidth

    // --- FORMAT √áEVƒ∞Rƒ∞Cƒ∞ ---
    function getWorkspaceLabel(numStr) {
        var fmt = config.format || "chinese";
        
        if (fmt === "roman") {
            var romans = ["", "I", "II", "III", "IV", "V", "VI", "VII", "VIII", "IX", "X"];
            var n = parseInt(numStr);
            if (!isNaN(n) && n >= 1 && n <= 10) return romans[n];
            return numStr;
        } 
        
        if (fmt === "chinese") {
            var map = {
                "1": "‰∏Ä", "2": "‰∫å", "3": "‰∏â", "4": "Âõõ", "5": "‰∫î",
                "6": "ÂÖ≠", "7": "‰∏É", "8": "ÂÖ´", "9": "‰πù", "10": "ÂçÅ"
            };
            return map[numStr] || numStr;
        }

        // Arabic (Default fallback)
        return numStr;
    }

    // --- DEV ƒ∞KON K√úT√úPHANESƒ∞ ---
    function getIcon(appId, title) {
        if (!appId) appId = "";
        if (!title) title = "";
        var c = appId.toLowerCase();
        var t = title.toLowerCase();

        // 1. TITLE (Ba≈ülƒ±k) E≈üle≈ütirmeleri
        if (t.includes("amazon")) return "Ôâ∞ ";
        if (t.includes("reddit")) return "ÔäÅ ";
        if (t.includes("gmail")) return "Û∞ä´ ";
        if (t.includes("whatsapp")) return "Ôà≤ ";
        if (t.includes("zapzap")) return "Ôà≤ ";
        if (t.includes("messenger")) return "Ôâü ";
        if (t.includes("facebook")) return "ÔÇö ";
        if (t.match(/chatgpt|deepseek|qwen/)) return "Û∞ö© ";
        if (t.includes("picture-in-picture")) return "Ôî¨ ";
        if (t.includes("youtube")) return "ÔÖ™ ";
        if (t.includes("cmus")) return "ÔÄÅ ";
        if (t.includes("virtualbox")) return "üíΩ ";
        if (t.includes("github")) return "ÔÑì ";
        if (t.match(/nvim ~|vim|nvim/)) return "ÓüÖ ";
        if (t.includes("figma")) return "ÓΩá ";
        if (t.includes("jira")) return "Óùú ";
        if (t.includes("x"))return "\ueb72";
          if (t.includes("google"))return "\ue7f0";
          if (t.includes("flow"))return "\ue69f";
        // G√∂zden ka√ßan ba≈ülƒ±klar i√ßin garanti kontrol
        if (t.includes("dolphin")) return "Û∞ù∞ ";
        if (t.includes("kwrite")) return "Óö• ";

        // 2. CLASS (Uygulama Kimliƒüi) E≈üle≈ütirmeleri
        if (c.match(/firefox|org\.mozilla\.firefox|librewolf|floorp|mercury-browser|cachy-browser/)) return "ÓùÖ ";
        if (c.match(/zen/)) return "Û∞∞∑ ";
        if (c.match(/waterfox|waterfox-bin/)) return "Ó∫ì ";
        if (c.match(/microsoft-edge/)) return "ÔäÇ ";
        if (c.match(/chromium|thorium|chrome/)) return "Ôâ® ";
        if (c.match(/brave-browser/)) return "ü¶Å ";
        if (c.match(/tor browser/)) return "Ôç± ";
        if (c.match(/firefox-developer-edition/)) return "ü¶ä ";

        if (c.match(/kitty|konsole/)) return "ÓØÜ ";
        if (c.match(/kitty-dropterm/)) return "ÔÑ† ";
        if (c.match(/com\.mitchellh\.ghostty/)) return " Óûï ";
        if (c.match(/org\.wezfurlong\.wezterm/)) return " Ó™Ö ";

        if (c.match(/thunderbird|thunderbird-esr|eu\.betterbird\.betterbird/)) return "Ôç∞ ";

        if (c.match(/telegram-desktop|org\.telegram\.desktop|io\.github\.tdesktop_x64\.tdesktop/)) return "Óàó ";
        if (c.match(/discord|webcord|vesktop/)) return "Ôáø ";
        if (c.match(/subl/)) return "Û∞Ö≥ ";
        if (c.match(/slack/)) return "ÔÜò ";

        if (c.match(/mpv/)) return "ÔçÆ ";
        if (c.match(/celluloid|zoom/)) return "ÔÄΩ ";
        if (c.match(/cider/)) return "Û∞éÜ ";
        if (c.match(/vlc/)) return "Û∞ïº ";
        if (c.match(/spotify/)) return "ÔÜº ";

        if (c.match(/virt-manager|\.virt-manager-wrapped/)) return "Ó≠ª ";
        if (c.match(/virtualbox manager/)) return "üíΩ ";
        if (c.match(/remmina/)) return "üñ•Ô∏è ";

        if (c.match(/vscode|code-url-handler|code-oss|codium|codium-url-handler|vscodium/)) return "Û∞®û ";
        if (c.match(/dev\.zed\.zed/)) return "Û∞µÅ ";
        if (c.match(/codeblocks/)) return "Û∞Ö© ";
        if (c.match(/mousepad/)) return "ÔÖú ";

        if (c.match(/libreoffice-writer/)) return "Óö• ";
        if (c.match(/libreoffice-startcenter/)) return "Û∞èÜ ";
        if (c.match(/libreoffice-calc/)) return "Ôçª ";
        if (c.match(/jetbrains-idea/)) return "Óûµ ";

        if (c.match(/obs|com\.obsproject\.studio/)) return "ÓÆß ";
        if (c.match(/polkit-gnome-authentication-agent-1/)) return "Û∞íÉ ";
        if (c.match(/nwg-look/)) return "Ó≠í ";
        if (c.match(/pavucontrol|org\.pulseaudio\.pavucontrol/)) return "Û±°´ ";
        if (c.match(/steam/)) return "ÔÜ∂ ";

        // Dolphin ve Kwrite burada tam isabet yakalanacak
        if (c.match(/thunar|nemo|dolphin/)) return "Û∞ù∞ ";
        if (c.match(/kwrite/)) return "Óö• ";

        if (c.match(/gparted/)) return "ÔÉá ";
        if (c.match(/gimp/)) return "Ôå∏ ";
        if (c.match(/emulator/)) return "üì± ";
        if (c.match(/android-studio/)) return "ÔÖª ";
        if (c.match(/org\.pipewire\.helvum/)) return "Û∞ìÉ ";
        if (c.match(/localsend/)) return "Ôá† ";
        if (c.match(/prusaslicer|ultimaker-cura|orcaslicer/)) return "Û∞πõ ";

        return "ÔÅô "; // Hi√ßbiri e≈üle≈ümezse
    }

    // --- Nƒ∞Rƒ∞ √áALI≈ûMA ALANI VERƒ∞LERƒ∞ ---
    property var activeWorkspaces: []
    property var monWsIds: []
    property var monWsMap: {}
    property string lastStateHash: ""

    Process { id: focusProc; command: [] }

    function switchToWorkspace(targetName) {
        if (CompositorService.isHyprland) {
            focusProc.command = ["hyprctl", "dispatch", "workspace", String(targetName)];
            focusProc.running = true;
        } else if (CompositorService.isNiri) {
            focusProc.command = ["niri", "msg", "action", "focus-workspace", String(targetName)];
            focusProc.running = true;
        } else if (CompositorService.isMango) {
            focusProc.command = ["mmsg", "-s", "-o", monitorName, "-t", String(targetName)];
            focusProc.running = true;
        }
    }

    function scrollWorkspaces(direction) {
        if (!workspaceRoot.scrollEnabled) return;
        var wss = workspaceRoot.activeWorkspaces.filter(w => !isNaN(parseInt(w.name)));
        if (wss.length < 2) return;
        
        var currentIndex = wss.findIndex(w => w.is_active);
        var validIndex = currentIndex === -1 ? 0 : currentIndex;
        // Direction pozitifse saƒüa (sonraki), negatifse sola (√∂nceki)
        var nextIndex = direction > 0 ? Math.min(validIndex + 1, wss.length - 1) : Math.max(validIndex - 1, 0);
        
        if (nextIndex !== validIndex) {
            switchToWorkspace(wss[nextIndex].name);
        }
    }

    Process {
        id: wsProc
        command: CompositorService.isHyprland ? ["hyprctl", "workspaces", "-j"] : (CompositorService.isMango ? ["mmsg", "-g", "-t"] : ["niri", "msg", "-j", "workspaces"])
        property string outputBuffer: ""
        stdout: SplitParser { onRead: (data) => wsProc.outputBuffer += data + "\n" }
        onExited: {
            if (wsProc.outputBuffer.trim() === "") return;
            try {
                if (CompositorService.isMango) {
                    // mmsg -g -t √ßƒ±ktƒ± formatƒ± (dwl IPC):
                    // DP-2 tag 1 1 2 1   ‚Üê monitor tag_num state clients focused
                    // DP-2 clients 5
                    // DP-2 tags 7 2 0
                    var lines = wsProc.outputBuffer.trim().split("\n");
                    var mWsIds = [];
                    var mWsMap = {};
                    for (var i = 0; i < lines.length; i++) {
                        var line = lines[i].trim();
                        if (line === "") continue;
                        var parts = line.split(/\s+/);
                        if (parts.length < 2) continue;

                        var lineMon = parts[0];
                        // Bu monit√∂re ait olmayan satƒ±rlarƒ± atla
                        if (lineMon !== monitorName) continue;

                        // "DP-2 tag 1 1 2 1" formatƒ±
                        if (parts[1] === "tag" && parts.length >= 6) {
                            var tagNum = parseInt(parts[2]);
                            var state = parseInt(parts[3]);
                            // State: 0=none, 1=active, 2=urgent
                            if (!isNaN(tagNum)) {
                                mWsIds.push(tagNum);
                                mWsMap[tagNum] = {
                                    id: tagNum,
                                    idx: tagNum,
                                    name: String(tagNum),
                                    is_active: state === 1,
                                    clients: parseInt(parts[4]) || 0,
                                    focused: parseInt(parts[5]) || 0
                                };
                            }
                        }
                        // "clients" ve "tags" satƒ±rlarƒ±nƒ± atlƒ±yoruz, per-tag yeterli
                    }
                    mWsIds.sort((a, b) => a - b);
                    workspaceRoot.monWsIds = mWsIds;
                    workspaceRoot.monWsMap = mWsMap;

                    // ToplevelManager √ºzerinden bu monit√∂rdeki t√ºm pencereleri bul
                    var monitorWindows = [];
                    var toplevels = ToplevelManager.toplevels;
                    var tlValues = toplevels.values || [];
                    for (var ti = 0; ti < tlValues.length; ti++) {
                        var tl = tlValues[ti];
                        var onThisScreen = false;
                        if (tl.screens) {
                            for (var si = 0; si < tl.screens.length; si++) {
                                if (tl.screens[si].name === monitorName) {
                                    onThisScreen = true;
                                    break;
                                }
                            }
                        }
                        if (onThisScreen) {
                            monitorWindows.push({
                                app_id: tl.appId || "",
                                title: tl.title || "",
                                is_active: tl.activated || false
                            });
                        }
                    }

                    // Sonu√ß listesini olu≈ütur
                    var result = [];
                    var existingNames = [];
                    for (var j = 0; j < mWsIds.length; j++) {
                        var id = mWsIds[j];
                        var ws = mWsMap[id];
                        ws.windows = [];
                        ws.groupedWindows = [];
                        ws.winCount = ws.clients || 0;

                        if (ws.is_active && monitorWindows.length > 0) {
                            // Aktif tag: ws.clients sayƒ±sƒ± kadar pencere g√∂ster (mmsg'den gelen doƒüru sayƒ±)
                            var tagClientCount = ws.clients || 0;
                            // Aktif (odaklanmƒ±≈ü) pencereyi √∂ne al
                            var sorted = monitorWindows.slice().sort(function(a, b) { return (b.is_active ? 1 : 0) - (a.is_active ? 1 : 0); });
                            // mmsg'nin bildirdiƒüi client sayƒ±sƒ± kadar pencere al
                            var visibleWindows = tagClientCount > 0 ? sorted.slice(0, tagClientCount) : sorted;
                            // Aktif tag: ToplevelManager'dan pencereleri al ve √∂nbelleƒüe kaydet
                            var grps = {};
                            for (var wi = 0; wi < visibleWindows.length; wi++) {
                                var w = visibleWindows[wi];
                                var appKey = w.app_id || w.title || "unknown";
                                var icon = getIcon(w.app_id, w.title);
                                ws.windows.push(w);
                                if (!grps[appKey]) {
                                    grps[appKey] = { icon: icon, active: w.is_active, count: 1 };
                                } else {
                                    grps[appKey].count++;
                                    if (w.is_active) grps[appKey].active = true;
                                }
                            }
                            var groupedArr = [];
                            for (var g in grps) {
                                if (!workspaceRoot.groupApps || ws.is_active) {
                                    for (var gi = 0; gi < grps[g].count; gi++) {
                                        groupedArr.push({ icon: grps[g].icon, active: grps[g].active });
                                    }
                                } else {
                                    groupedArr.push(grps[g]);
                                }
                            }
                            ws.groupedWindows = groupedArr;
                            // √ñnbelleƒüe kaydet
                            var cache = workspaceRoot.tagIconCache;
                            cache[id] = { windows: ws.windows, groupedWindows: ws.groupedWindows };
                            workspaceRoot.tagIconCache = cache;
                        } else if (ws.winCount > 0 && workspaceRoot.tagIconCache[id]) {
                            // ƒ∞naktif tag: √∂nbellekten y√ºkle
                            var cached = workspaceRoot.tagIconCache[id];
                            ws.windows = cached.windows;
                            ws.groupedWindows = cached.groupedWindows;
                        }

                        result.push(ws);
                        existingNames.push(String(ws.name));
                    }
                    // Bo≈ü da olsa ilk 9 tag'ƒ± her zaman g√∂ster
                    for (var k = 1; k <= 9; k++) {
                        var strK = String(k);
                        if (existingNames.indexOf(strK) === -1) {
                            result.push({ id: strK, idx: k, name: strK, is_active: false, winCount: 0, clients: 0, windows: [], groupedWindows: [] });
                        }
                    }
                    result.sort(function(a, b) {
                        var numA = parseInt(a.name);
                        var numB = parseInt(b.name);
                        if (!isNaN(numA) && !isNaN(numB)) return numA - numB;
                        return 0;
                    });
                    var currentState = JSON.stringify(result);
                    if (workspaceRoot.lastStateHash !== currentState) {
                        workspaceRoot.lastStateHash = currentState;
                        workspaceRoot.activeWorkspaces = result;
                    }
                } else {
                var allWs = JSON.parse(wsProc.outputBuffer);
                var mWsIds = [];
                var mWsMap = {};
                for (var i = 0; i < allWs.length; i++) {
                    var ws = allWs[i];
                    if (CompositorService.isHyprland) {
                        // Hyprland Logic
                        if (ws.monitor === monitorName) {
                            mWsIds.push(ws.id);
                            mWsMap[ws.id] = {
                                id: ws.id,
                                idx: ws.id,
                                name: ws.name ? ws.name : String(ws.id),
                                is_active: ws.id === activeHyprlandWorkspaceId
                            };
                        }
                    } else {
                        // Niri Logic
                        if (ws.output === monitorName) {
                            mWsIds.push(ws.id);
                            mWsMap[ws.id] = {
                                id: ws.id,
                                idx: ws.idx !== undefined ? ws.idx : (i + 1),
                                name: ws.name ? ws.name : String(ws.idx !== undefined ? ws.idx : (i + 1)),
                                is_active: ws.is_focused ?? false
                            };
                        }
                    }
                }
                mWsIds.sort((a, b) => mWsMap[a].idx - mWsMap[b].idx);
                workspaceRoot.monWsIds = mWsIds;
                workspaceRoot.monWsMap = mWsMap;
                } // end of else (Hyprland/Niri)
            } catch (e) {}
            wsProc.outputBuffer = "";
        }
    }

    property int activeHyprlandWorkspaceId: -1

    Process {
        id: activeWsProc
        command: ["hyprctl", "activeworkspace", "-j"]
        property string outputBuffer: ""
        stdout: SplitParser { onRead: (data) => activeWsProc.outputBuffer += data }
        onExited: {
            if (activeWsProc.outputBuffer.trim() === "") return;
            try {
                var o = JSON.parse(activeWsProc.outputBuffer);
                workspaceRoot.activeHyprlandWorkspaceId = o.id;
            } catch (e) {}
            activeWsProc.outputBuffer = "";
        }
    }

    Process {
        id: winProc
        command: CompositorService.isHyprland ? ["hyprctl", "clients", "-j"] : (CompositorService.isMango ? ["echo", ""] : ["niri", "msg", "-j", "windows"])
        property string outputBuffer: ""
        stdout: SplitParser { onRead: (data) => winProc.outputBuffer += data }
        onExited: {
            // Mango: wsProc.onExited i√ßinde zaten i≈üleniyor, burada sadece √ßƒ±k
            if (CompositorService.isMango) {
                winProc.outputBuffer = "";
                return;
            }
            if (winProc.outputBuffer.trim() === "") return;
            try {
                var allWindows = JSON.parse(winProc.outputBuffer);
                var wsMap = workspaceRoot.monWsMap ?? {};
                var mWsIds = workspaceRoot.monWsIds ?? [];

                for (var wsId in wsMap) {
                    wsMap[wsId].windows = [];
                    wsMap[wsId].winCount = 0;
                }

                for (var i = 0; i < allWindows.length; i++) {
                    var win = allWindows[i];
                    if (CompositorService.isHyprland) {
                        // Hyprland logic
                        if (win.workspace && mWsIds.includes(win.workspace.id)) {
                            var wId = win.workspace.id;
                            if (wsMap[wId]) {
                                var isFocused = win.focusHistoryID === 0; // Simple approximation for active hyprland client
                                wsMap[wId].windows.push({app_id: win.class, title: win.title, is_active: isFocused});
                                wsMap[wId].winCount++;
                            }
                        }
                    } else {
                        // Niri Logic
                        if (mWsIds.includes(win.workspace_id)) {
                            var wId = win.workspace_id;
                            if (wsMap[wId]) {
                                wsMap[wId].windows.push({app_id: win.app_id, title: win.title, is_active: win.is_focused});
                                wsMap[wId].winCount++;
                                if (win.is_focused) wsMap[wId].is_active = true;
                            }
                        }
                    }
                }

                // Uygulamalarƒ± gruplama i≈ülemi
                for (var key in wsMap) {
                    var grps = {};
                    var wins = wsMap[key].windows;
                    for (var j = 0; j < wins.length; j++) {
                        var w = wins[j];
                        var appKey = w.app_id || w.title || "unknown";
                        var icon = getIcon(w.app_id, w.title);
                        if (!grps[appKey]) {
                            grps[appKey] = { icon: icon, active: w.is_active, count: 1 };
                        } else {
                            grps[appKey].count++;
                            if (w.is_active) grps[appKey].active = true;
                        }
                    }
                    var groupedArr = [];
                    for (var g in grps) {
                        // Eƒüer gruplama kapalƒ±ysa ve workspace aktifse, grup yerine ayrƒ± ayrƒ± g√∂ster
                        if (!workspaceRoot.groupApps || wsMap[key].is_active) {
                            for (var k=0; k<grps[g].count; k++) {
                                groupedArr.push({
                                    icon: grps[g].icon,
                                    active: grps[g].active
                                });
                            }
                        } else {
                            groupedArr.push(grps[g]);
                        }
                    }
                    wsMap[key].groupedWindows = groupedArr;
                }

                var result = [];
                var existingNames = [];
                for (var j = 0; j < mWsIds.length; j++) {
                    var id = mWsIds[j];
                    result.push(wsMap[id]);
                    existingNames.push(String(wsMap[id].name));
                }

                // Bo≈ü da olsa ilk 5 √ßalƒ±≈üma alanƒ±nƒ± her zaman g√∂ster
                for (var k = 1; k <= 5; k++) {
                    var strK = String(k);
                    if (existingNames.indexOf(strK) === -1) {
                        result.push({ id: strK, idx: k, name: strK, is_active: false, winCount: 0, windows: [], groupedWindows: [] });
                    }
                }

                result.sort(function(a, b) {
                    var numA = parseInt(a.name);
                    var numB = parseInt(b.name);
                    if (!isNaN(numA) && !isNaN(numB)) return numA - numB;
                    return 0;
                });

                var currentState = JSON.stringify(result);
                if (workspaceRoot.lastStateHash !== currentState) {
                    workspaceRoot.lastStateHash = currentState;
                    workspaceRoot.activeWorkspaces = result;
                }
            } catch (e) {}
            winProc.outputBuffer = "";
        }
    }

    Timer {
        interval: 500; running: true; repeat: true;
        onTriggered: { 
            if (CompositorService.isHyprland) { activeWsProc.running = true; }
            wsProc.running = true; 
            winProc.running = true; 
        }
    }

    // Scroll Alanƒ±
    MouseArea {
        anchors.fill: parent
        acceptedButtons: Qt.NoButton
        
        onWheel: wheel => {
            if (!workspaceRoot.scrollEnabled || scrollInProgress) return;

            var delta = wheel.angleDelta.y;
            workspaceRoot.mouseAccumulator += delta;
            if (Math.abs(workspaceRoot.mouseAccumulator) < 120) return;
            var direction = workspaceRoot.mouseAccumulator < 0 ? 1 : -1;
            workspaceRoot.scrollWorkspaces(direction);
            
            workspaceRoot.scrollInProgress = true;
            scrollCooldown.restart();
            workspaceRoot.mouseAccumulator = 0;
        }
    }

    // --- G√ñRSEL D√úZEN (≈ûƒ±k Hap Tasarƒ±mƒ±) ---
    Row {
        id: wsRow
        anchors.centerIn: parent
        spacing: 8

        Repeater {
            model: workspaceRoot.activeWorkspaces
            delegate: Rectangle {
                id: wsBox
                property var wsData: modelData
                property bool isActive: wsData.is_active
                property int winCount: wsData.winCount

                // ƒ∞√ßeriƒüe g√∂re dinamik geni≈üleyen boyut
                implicitWidth: wsContent.implicitWidth + 24
                height: 34
                radius: style === "square" ? 6 : 17

                // STƒ∞L MANTIƒûI
                color: {
                   if (style === "fill") {
                       if (isActive) return activeColor;
                       return isTransparent ? "transparent" : Theme.surface;
                   }
                   if (style === "square" || style === "circle") {
                       return isActive ? Qt.rgba(activeColor.r, activeColor.g, activeColor.b, 0.4) : Qt.rgba(Theme.surface.r, Theme.surface.g, Theme.surface.b, 0.2); 
                   }
                   return "transparent";
                }

                border.width: (style === "outline" || style === "square" || style === "circle") ? 2 : 0
                border.color: {
                    if (style === "outline" || style === "square" || style === "circle") return isActive ? activeColor : (isTransparent ? "transparent" : Theme.surface);
                    return "transparent";
                }

                Behavior on color { ColorAnimation { duration: 200 } }
                Behavior on implicitWidth { NumberAnimation { duration: 250; easing.type: Easing.OutQuint } }

                Row {
                    id: wsContent
                    anchors.centerIn: parent
                    spacing: 8

                    // NUMARA (Formatlƒ±)
                    Text {
                        text: getWorkspaceLabel(wsData.name)
                        color: isActive ? Theme.workspaceActiveTextColor : Theme.text
                        font.bold: true
                        font.pixelSize: 14
                        font.family: "JetBrainsMono Nerd Font"
                        anchors.verticalCenter: parent.verticalCenter
                    }

                    // ARAYA EKLENEN ƒ∞NCE √áƒ∞ZGƒ∞ (Ayƒ±rƒ±cƒ±)
                    Rectangle {
                        width: 1
                        height: 14
                        color: (isActive ? Theme.workspaceActiveTextColor : Theme.text) // Dinamik renk
                        opacity: 0.25
                        anchors.verticalCenter: parent.verticalCenter
                        visible: winCount > 0 && workspaceRoot.showApps
                    }

                    // UYGULAMA ƒ∞KONLARI
                    Row {
                        spacing: 6
                        anchors.verticalCenter: parent.verticalCenter
                        visible: winCount > 0 && workspaceRoot.showApps

                        Repeater {
                            model: wsData.groupedWindows
                            
                            Item {
                                width: iconText.implicitWidth
                                height: workspaceRoot.iconSize + 4
                                
                                Text {
                                    id: iconText
                                    text: modelData.icon
                                    color: (isActive || modelData.active) ? (isActive ? Theme.workspaceActiveTextColor : Theme.primary) : Theme.text
                                    opacity: modelData.active ? 1.0 : (isActive ? 0.9 : 0.6)
                                    font.pixelSize: workspaceRoot.iconSize
                                    font.family: "JetBrainsMono Nerd Font"
                                    anchors.centerIn: parent
                                }
                                
                                // Gruplama balonu (√∂r: 2 tane aynƒ± app varsa)
                                Rectangle {
                                    visible: (modelData.count !== undefined && modelData.count > 1) && !isActive
                                    width: 12
                                    height: 12
                                    radius: 6
                                    color: Theme.surface
                                    border.color: Theme.text
                                    border.width: 1
                                    anchors.right: parent.right
                                    anchors.rightMargin: -6
                                    anchors.bottom: parent.bottom
                                    anchors.bottomMargin: -2
                                    z: 2

                                    Text {
                                        anchors.centerIn: parent
                                        text: modelData.count !== undefined ? String(modelData.count) : ""
                                        font.pixelSize: 8
                                        color: Theme.text
                                        font.bold: true
                                    }
                                }
                            }
                        }
                    }
                }

                // ALT √áƒ∞ZGƒ∞ (Underline Stili)
                Rectangle {
                    anchors.bottom: parent.bottom
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: parent.width * 0.6
                    height: 3
                    radius: 1.5
                    color: activeColor
                    visible: style === "underline" && isActive
                }

                // NOKTA (Dot Stili)
                Rectangle {
                    anchors.top: parent.bottom
                    anchors.topMargin: 2
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: 4
                    height: 4
                    radius: 2
                    color: activeColor
                    visible: style === "dot" && isActive
                }

                // √úST √áƒ∞ZGƒ∞ (Overline Stili)
                Rectangle {
                    anchors.top: parent.top
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: parent.width * 0.6
                    height: 3
                    radius: 1.5
                    color: activeColor
                    visible: style === "overline" && isActive
                }

                // YAN √áƒ∞ZGƒ∞ (Pipe Stili)
                Rectangle {
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    anchors.leftMargin: 4
                    width: 3
                    height: parent.height * 0.6
                    radius: 1.5
                    color: activeColor
                    visible: style === "pipe" && isActive
                }

                // Tƒ±klama Alanƒ±
                MouseArea {
                    anchors.fill: parent
                    cursorShape: Qt.PointingHandCursor
                    onClicked: workspaceRoot.switchToWorkspace(wsData.name)
                }
            }
        }
    }
}
